; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Aeneas_Installer"
#define MyAppVersion "1.5.0.3"
#define MyAppPublisher "Daniel Bair"
#define MyAppURL "http://www.danielbair.com/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A15A9B64-C0EF-4E13-926C-0C60A8FA9FBC}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
;UsePreviousAppDir=no
DefaultDirName=C:\{#MyAppName}
DisableDirPage=no
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
DisableProgramGroupPage=yes
LicenseFile=LICENSE.txt
InfoBeforeFile=README.txt
OutputDir=.\
OutputBaseFilename=Aeneas_Installer
Compression=lzma
SolidCompression=yes
ChangesEnvironment=yes
;SignTool=mysigntool

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "C:\Python27\Scripts;{olddata}"; Components: python; Check: NeedsAddPath('C:\Python27\Scripts')
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "C:\Python27\;{olddata}"; Components: python; Check: NeedsAddPath('C:\Python27\')
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};{pf32}\FFmpeg\bin"; Components: ffmpeg; Check: NeedsAddPath(ExpandConstant('{pf32}\FFmpeg\bin'))
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};{pf32}\eSpeak\command_line"; Components: espeak; Check: NeedsAddPath(ExpandConstant('{pf32}\eSpeak\command_line'))

[Components]
Name: "ffmpeg"; Description: "Install FFmpeg 3.0.2"; ExtraDiskSpaceRequired: 111181824; Types: full custom;
Name: "espeak"; Description: "Install eSpeak 1.48.04"; ExtraDiskSpaceRequired: 11223040; Types: full custom;
Name: "python"; Description: "Install Python 2.7.11"; ExtraDiskSpaceRequired: 106450944; Types: full custom;
;Name: "vcfp"; Description: "Install Visual C++ for Python"; ExtraDiskSpaceRequired: 400912384; Types: full compact custom;
Name: "bs4"; Description: "Install Python Module BeautifulSoup4 4.4.1"; ExtraDiskSpaceRequired: 3400000; Types: full compact custom;
Name: "lxml"; Description: "Install Python Module Lxml 3.6.0"; ExtraDiskSpaceRequired: 0; Types: full compact custom;
Name: "numpy"; Description: "Install Python Module NumPy 1.10.1"; ExtraDiskSpaceRequired: 0; Types: full compact custom;
Name: "aeneas"; Description: "Install Python Module Aeneas 1.5.0.3"; ExtraDiskSpaceRequired: 0; Types: full compact custom;

[Files]
Source: "aeneas-1.5.0.3-cp27-cp27m-win32.whl"; DestDir: "{app}"; Components: aeneas; Flags: ignoreversion
Source: "aeneas_check_setup.bat"; DestDir: "{app}"; Components: aeneas; Flags: ignoreversion
Source: "beautifulsoup4-4.4.1-py2-none-any.whl"; DestDir: "{app}"; Components: bs4; Flags: ignoreversion
Source: "install_packages.bat"; DestDir: "{app}"; Components: aeneas; Flags: ignoreversion
Source: "lxml-3.6.0-cp27-none-win32.whl"; DestDir: "{app}"; Components: lxml; Flags: ignoreversion
Source: "numpy-1.10.1-cp27-cp27m-win32.whl"; DestDir: "{app}"; Components: numpy; Flags: ignoreversion
Source: "pip-8.1.2-py2.py3-none-any.whl"; DestDir: "{app}"; Components: python; Flags: ignoreversion
Source: "python-2.7.11.msi"; DestDir: "{app}"; Components: python; Flags: ignoreversion
Source: "setup_espeak-1.48.04.exe"; DestDir: "{app}"; Components: espeak; Flags: ignoreversion
Source: "setup_ffmpeg-3.0.2.exe"; DestDir: "{app}"; Components: ffmpeg; Flags: ignoreversion
;Source: "vcforpython27.msi"; DestDir: "{app}"; Components: aeneas; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\setup_ffmpeg-3.0.2.exe"; Parameters: "/SILENT"; Description: "Install FFmpeg 3.0.2"; Components: ffmpeg; Flags: shellexec waituntilterminated
Filename: "{app}\setup_espeak-1.48.04.exe"; Parameters: "/SILENT"; Description: "Install eSpeak 1.48.04"; Components: espeak; Flags: shellexec waituntilterminated
Filename: "{app}\python-2.7.11.msi"; Parameters: "/PASSIVE"; Description: "Install Python 2.7.11"; Components: python; Flags: shellexec waituntilterminated
;Filename: "{app}\vcforpython27.msi"; Parameters: "/PASSIVE"; Description: "Install Visual C++ for Python 2.7.11"; Components: python; Flags: shellexec waituntilterminated
Filename: "{app}\install_packages.bat"; Description: "Install Aeneas 1.5.0.3 and dependencies"; Components: aeneas; Flags: shellexec waituntilterminated
Filename: "{app}\aeneas_check_setup.bat"; Description: "Check Aeneas Setup"; Components: aeneas; Flags: shellexec waituntilterminated

[UninstallRun]
Filename: "{pf32}\FFmpeg\unins000.exe"; Parameters: "/SILENT"; Components: ffmpeg; Flags: shellexec waituntilterminated
Filename: "{pf32}\eSpeak\unins000.exe"; Parameters: "/SILENT"; Components: espeak; Flags: shellexec waituntilterminated
Filename: "{sys}\MSIEXEC.EXE"; Parameters: "/PASSIVE /X {app}\python-2.7.11.msi"; Components: python; Flags: shellexec waituntilterminated
;Filename: "RMDIR"; Parameters: "/S /Q C:\Python27"; Components: aeneas; Flags: shellexec waituntilterminated

[UninstallDelete]
Type: filesandordirs; Name: "C:\Python27"; Components: python
Type: filesandordirs; Name: "{app}";

[Code]
function NeedsAddPath(Param: string): boolean;
var
  OrigPath: string;
begin
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment',
    'Path', OrigPath)
  then begin
    Result := True;
    exit;
  end;
  // look for the path with leading and trailing semicolon
  // Pos() returns 0 if not found
  Result := Pos(';' + Param + ';', ';' + OrigPath + ';') = 0;
end;
